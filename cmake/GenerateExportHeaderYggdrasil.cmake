function(generate_export_header_yggdrasil target)
  include(GenerateExportHeader)
  set(options EMPTY)
  set(oneValueArgs PREFIX_NAME BASE_NAME EXPORT_MACRO_NAME EXPORT_FILE_NAME
      DEPRECATED_MACRO_NAME NO_EXPORT_MACRO_NAME
      NO_DEPRECATED_MACRO_NAME CUSTOM_CONTENT_FROM_VARIABLE
      INCLUDE_GUARD_NAME)
  cmake_parse_arguments(ARGS "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
  if (NOT ARGS_BASE_NAME)
    set(ARGS_BASE_NAME YGG_API)
  endif()
  string(TOUPPER ${ARGS_BASE_NAME} BASE_NAME_UPPER)
  string(TOLOWER ${ARGS_BASE_NAME} BASE_NAME_LOWER)
  if (NOT ARGS_EXPORT_MACRO_NAME)
    set(ARGS_EXPORT_MACRO_NAME ${ARGS_PREFIX_NAME}YGG_API)
  endif()
  if (NOT ARGS_EXPORT_FILE_NAME)
    set(ARGS_EXPORT_FILE_NAME YggInterface_export.h)
  endif()
  if (NOT ARGS_INCLUDE_GUARD_NAME)
    set(ARGS_INCLUDE_GUARD_NAME "${ARGS_EXPORT_MACRO_NAME}_H")
  endif()
  if (NOT ARGS_NO_EXPORT_MACRO_NAME)
    set(ARGS_NO_EXPORT_MACRO_NAME "${ARGS_PREFIX_NAME}${BASE_NAME_UPPER}_NO_EXPORT")
  endif()
  if (NOT ARGS_DEPRECATED_MACRO_NAME)
    set(ARGS_DEPRECATED_MACRO_NAME "${ARGS_PREFIX_NAME}${BASE_NAME_UPPER}_DEPRECATED")
  endif()
  if (NOT ARGS_NO_DEPRECATED_MACRO_NAME)
    set(ARGS_NO_DEPRECATED_MACRO_NAME "${ARGS_PREFIX_NAME}${BASE_NAME_UPPER}_NO_DEPRECATED")
  endif()
  
  if (ARGS_EMPTY)
    set(EXPORT_MACRO_NAME "${ARGS_EXPORT_MACRO_NAME}")
    set(INCLUDE_GUARD_NAME "${ARGS_INCLUDE_GUARD_NAME}")
    set(NO_EXPORT_MACRO_NAME "${ARGS_NO_EXPORT_MACRO_NAME}")
    set(DEPRECATED_MACRO_NAME "${ARGS_DEPRECATED_MACRO_NAME}")
    set(NO_DEPRECATED_MACRO_NAME "${ARGS_NO_DEPRECATED_MACRO_NAME}")
    set(CUSTOM_CONTENT)
    if (ARGS_CUSTOM_CONTENT_FROM_VARIABLE)
      set(CUSTOM_CONTENT "${${ARGS_CUSTOM_CONTENT_FROM_VARIABLE}}")
    endif()
    configure_file(
      ${CMAKE_CURRENT_FUNCTION_LIST_DIR}/CMakeAddFortranSubdirectory/YggInterface_export.h.in
      ${CMAKE_CURRENT_BINARY_DIR}/${ARGS_EXPORT_FILE_NAME}
      @ONLY)
  else()
    generate_export_header(
      ${target}
      PREFIX_NAME ${ARGS_PREFIX_NAME}
      BASE_NAME ${ARGS_BASE_NAME}
      EXPORT_MACRO_NAME ${ARGS_EXPORT_MACRO_NAME}
      EXPORT_FILE_NAME ${ARGS_EXPORT_FILE_NAME}
      DEPRECATED_MACRO_NAME ${ARGS_DEPRECATED_MACRO_NAME}
      NO_EXPORT_MACRO_NAME ${ARGS_NO_EXPORT_MACRO_NAME}
      NO_DEPRECATED_MACRO_NAME ${ARGS_NO_DEPRECATED_MACRO_NAME}
      CUSTOM_CONTENT_FROM_VARIABLE ${ARGS_CUSTOM_CONTENT_FROM_VARIABLE}
      INCLUDE_GUARD_NAME ${ARGS_INCLUDE_GUARD_NAME}
      ${ARGS_UNPARSED_ARGUMENTS})
  endif()
  list(APPEND YGG_INSTALL_INCLUDES
       ${CMAKE_CURRENT_BINARY_DIR}/${ARGS_EXPORT_FILE_NAME})
  set(YGG_INSTALL_INCLUDES ${YGG_INSTALL_INCLUDES} PARENT_SCOPE)
  target_include_directories(
       ${target} PUBLIC
       $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>)
endfunction()