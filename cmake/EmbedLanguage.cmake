function(embed_language LANGUAGE)
  include(AddYggInterface)
  set(options DISABLE_BY_DEFAULT VERBOSE)
  set(oneValueArgs INCLUDE_DIRS_VAR LIBRARY_VAR LIBRARY_DIR_VAR
      FIND_METHOD DEPENDENCY)
  set(multiValueArgs DEFINITIONS FIND_ARGS DEPENDENCY_PROPERTIES)
  cmake_parse_arguments(ARGS "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
  set(DEFAULT_ENABLED ON)
  if (ARGS_DISABLE_BY_DEFAULT)
    set(DEFAULT_ENABLED OFF)
  endif()
  option(YGG_EMBED_${LANGUAGE} "Build interfaces to support ${LANGUAGE} embedding" ${DEFAULT_ENABLED})
  if (NOT ARGS_INCLUDE_DIRS_VAR)
    set(ARGS_INCLUDE_DIRS_VAR ${LANGUAGE}_INCLUDE_DIRS)
  endif()
  if (NOT ARGS_LIBRARY_VAR)
    set(ARGS_LIBRARY_VAR ${LANGUAGE}_LIBRARY)
  endif()
  if (NOT ARGS_LIBRARY_DIR_VAR)
    set(ARGS_LIBRARY_DIR_VAR ${LANGUAGE}_LIBRARY_DIR)
  endif()
  if (NOT ARGS_DEPENDENCY)
    set(ARGS_DEPENDENCY ${LANGUAGE})
  endif()
  string(TOUPPER ${LANGUAGE} LANGUAGE_UPPER)
  if (NOT YGG_EMBED_${LANGUAGE})
    message(STATUS "${LANGUAGE} embedding disabled")
    list(APPEND YGG_INSTALL_DEFS -DYGGDRASIL_DISABLE_EMBEDDED_${LANGUAGE_UPPER})
    if (LANGUAGE STREQUAL "Python")
      list(APPEND YGG_INSTALL_DEFS -DYGGDRASIL_DISABLE_PYTHON_C_API)
    endif()
  else()
    if (LANGUAGE STREQUAL "Python")
      list(APPEND ARGS_DEPENDENCY_PROPERTIES Python_PREFIX Python_FOUND)
      if (Python_PREFIX)
        set(ARGS_DEPENDENCY ${Python_PREFIX})
      endif()
    endif()
    if (ARGS_FIND_METHOD)
      cmake_language(CALL ${ARGS_FIND_METHOD} ${ARGS_FIND_ARGS})
    else()
      find_yggdrasil_dependency(
        ${ARGS_DEPENDENCY}
	ADDITIONAL_PROPERTIES ${ARGS_DEPENDENCY_PROPERTIES}
      )
    endif()
    if(${ARGS_DEPENDENCY}_FOUND)
      list(APPEND YGG_INSTALL_DEPS ${ARGS_DEPENDENCY})
      list(APPEND YGG_INSTALL_DEFS -DYGG_EMBED_${LANGUAGE_UPPER})
      if (${ARGS_INCLUDE_DIRS_VAR})
        list(APPEND DEPS_INCLUDE_DIRS ${${ARGS_INCLUDE_DIRS_VAR}})
      endif()
      if (${ARGS_LIBRARY_VAR})
        list(APPEND DEPS_LIBRARIES ${${ARGS_LIBRARY_VAR}})
      endif()
      if (${ARGS_LIBRARY_DIR_VAR})
        list(APPEND DEPS_LIBRARY_DIRS ${${ARGS_LIBRARY_DIR_VAR}})
      endif()
      if (ARGS_DEFINITIONS)
        list(APPEND YGG_INSTALL_DEFS ${ARGS_DEFINITIONS})
      endif()
      if (LANGUAGE STREQUAL "Python")
        list(APPEND DEPS_LIBRARIES ${Python_PREFIX}::NumPy)
	list(APPEND DEPS_LIBRARIES_Python ${Python_PREFIX}::Python)
      endif()
    else()
      message(STATUS "${LANGUAGE} not found, embedding will be disabled")
      set(YGG_EMBED_${LANGUAGE} OFF)
      list(APPEND YGG_INSTALL_DEFS -DYGGDRASIL_DISABLE_EMBEDDED_${LANGUAGE_UPPER})
      if (LANGUAGE STREQUAL "Python")
        list(APPEND YGG_INSTALL_DEFS -DYGGDRASIL_DISABLE_PYTHON_C_API)
      endif()
    endif()
    propagate_cmake_library_variables(
      "^${ARGS_DEPENDENCY}*" ${ARGS_DEPENDENCY_PROPERTIES}
    )
    if(ARGS_VERBOSE)
      dump_cmake_variables(REGEX "^${ARGS_DEPENDENCY}*" VERBOSE)
    endif()
  endif()
  list(APPEND YGG_EMBEDDED_LANGUAGES ${LANGUAGE})
  propagate_cmake_variables(
    YGG_EMBEDDED_LANGUAGES
    YGG_EMBED_${LANGUAGE}
    DEPS_INCLUDE_DIRS
    DEPS_LIBRARIES
    DEPS_LIBRARIES_Python
    DEPS_LIBRARY_DIRS
    YGG_INSTALL_DEPS
    YGG_INSTALL_DEFS
  )
endfunction()