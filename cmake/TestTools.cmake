function(set_tests_environment TEST_NAME)
  include(BuildTools)
  set(options FOR_GTEST_DISCOVER_TESTS)
  set(oneValueArgs DIRECTORY OUTPUT_PROPERTIES)
  set(multiValueArgs VARIABLES)
  cmake_parse_arguments(ARGS "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
  if(NOT ARGS_VARIABLES)
    return()
  endif()
  if(ARGS_UNPARSED_ARGUMENTS)
    list(APPEND TEST_NAME ${ARGS_UNPARSED_ARGUMENTS})
  endif()
  if(NOT ARGS_DIRECTORY)
    set(ARGS_DIRECTORY .)
  endif()
  message(STATUS "ARGS_VARIABLES = ${ARGS_VARIABLES}")
  if(ARGS_FOR_GTEST_DISCOVER_TESTS)
    if(NOT ARGS_OUTPUT_PROPERTIES)
      message(FATAL_ERROR "OUTPUT_PROPERTIES must be provided for tests discovered via gtest_discover_tests as set_tests_properties will not work")
    endif()
    foreach(var ${ARGS_VARIABLES})
      list(APPEND properties ENVIRONMENT "${var}")
    endforeach()
  else()
    set(properties "${ARGS_VARIABLES}")
  endif()
  message(STATUS "Setting \'${TEST_NAME}\' test properties: ${properties}")
  if(ARGS_OUTPUT_PROPERTIES)
    set(${ARGS_OUTPUT_PROPERTIES} "${properties}")
    set(${ARGS_OUTPUT_PROPERTIES} "${properties}" PARENT_SCOPE)
    message(STATUS "skipping set_tests_properties ${ARGS_OUTPUT_PROPERTIES} = ${${ARGS_OUTPUT_PROPERTIES}}")
  else()
    list(GET TEST_NAME 0 FIRST_TEST)
    if(NOT TEST ${FIRST_TEST})
      message(FATAL_ERROR "\'${TEST_NAME}\' Is not a test")
    endif()
    list(LENGTH properties properties_LEN)
    message(STATUS "Calling set_tests_properties ${properties} [len = ${properties_LEN}]")
    if (ARGS_FOR_GTEST_DISCOVER_TESTS)
      set_tests_properties(
        ${TEST_NAME} PROPERTIES ${properties}
      )
    else()
      set_tests_properties(
        ${TEST_NAME} PROPERTIES ENVIRONMENT "${properties}"
      )
    endif()
  endif()
  # There is a bug which prevents environment variables from
  # being available during test discovery on windows
  # https://gitlab.kitware.com/cmake/cmake/-/issues/21453
  if(WIN32 AND NOT MSVC)
    string(REPLACE "\\" "\\\\" ARGS_VARIABLES "${ARGS_VARIABLES}")
    configure_env_injection(
      DIRECTORY ${ARGS_DIRECTORY}
      VARIABLES ${ARGS_VARIABLES}
    )
  endif()
endfunction()


function(set_tests_runtime_paths TEST_NAME)
  include(BuildTools)
  set(options PREPEND ADD_CMAKE_PREFIX_PATHS FOR_GTEST_DISCOVER_TESTS)
  set(oneValueArgs PATH_VARIABLE OUTPUT_PROPERTIES ESCAPE_LEVEL)
  set(multiValueArgs PATHS ADDITIONAL_ENV_VARIABLES)
  cmake_parse_arguments(ARGS "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
  if(ARGS_ADD_CMAKE_PREFIX_PATHS)
    get_runtime_directory_suffix(runtime_suffix)
    foreach(d ${CMAKE_PREFIX_PATH})
      cmake_path(APPEND d ${runtime_suffix})
      list(APPEND ARGS_PATHS ${d})
    endforeach()
  endif()
  if(NOT ARGS_ESCAPE_LEVEL)
    set(ARGS_ESCAPE_LEVEL 0)
  endif()
  math(EXPR ARGS_ESCAPE_LEVEL "${ARGS_ESCAPE_LEVEL}+3")
  if(ARGS_FOR_GTEST_DISCOVER_TESTS)
    math(EXPR ARGS_ESCAPE_LEVEL "${ARGS_ESCAPE_LEVEL}+1")
  endif()
  # if(ARGS_OUTPUT_PROPERTIES)
  #   math(EXPR ARGS_ESCAPE_LEVEL "${ARGS_ESCAPE_LEVEL}+1")
  # endif()
  if(ARGS_PATHS)
    if(NOT ARGS_PATH_VARIABLE)
      get_runtime_environment_var(ARGS_PATH_VARIABLE)
    endif()
    if(ARGS_PREPEND)
      set(PREPEND_TOKEN PREPEND)
    endif()
    update_env_path(
      PATH_VARIABLE ${ARGS_PATH_VARIABLE}
      ${PREPEND_TOKEN}
      OUTPUT_VARIABLE UPDATED_PATHS
      PATHS ${ARGS_PATHS}
      ESCAPE_LEVEL ${ARGS_ESCAPE_LEVEL}
    )
    list(APPEND ARGS_ADDITIONAL_ENV_VARIABLES "${ARGS_PATH_VARIABLE}=${UPDATED_PATHS}")
  endif()
  if(ARGS_OUTPUT_PROPERTIES)
    list(APPEND ARGS_UNPARSED_ARGUMENTS OUTPUT_PROPERTIES ${ARGS_OUTPUT_PROPERTIES})
  endif()
  if(ARGS_FOR_GTEST_DISCOVER_TESTS)
    list(APPEND ARGS_UNPARSED_ARGUMENTS FOR_GTEST_DISCOVER_TESTS)
  endif()
  message(STATUS "ARGS_ADDITIONAL_ENV_VARIABLES = ${ARGS_ADDITIONAL_ENV_VARIABLES}")
  if(ARGS_ADDITIONAL_ENV_VARIABLES)
    set_tests_environment(
      ${TEST_NAME}
      ${ARGS_UNPARSED_ARGUMENTS}
      VARIABLES ${ARGS_ADDITIONAL_ENV_VARIABLES}
    )
  endif()
  if(ARGS_OUTPUT_PROPERTIES)
    message(STATUS "ARGS_OUTPUT_PROPERTIES = ${ARGS_OUTPUT_PROPERTIES}, ${ARGS_OUTPUT_PROPERTIES} = ${${ARGS_OUTPUT_PROPERTIES}}")
    set(${ARGS_OUTPUT_PROPERTIES} "${${ARGS_OUTPUT_PROPERTIES}}" PARENT_SCOPE)
  endif()
endfunction()
