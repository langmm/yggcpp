include(CMakeFindDependencyMacro)
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
include(SearchTools)
set(YGG_INSTALL_DEPS "@YGG_INSTALL_DEPS@")
foreach(dep IN LISTS YGG_INSTALL_DEPS)
  if (dep STREQUAL "Python3" OR dep STREQUAL "Python")
    set(PYTHON_PREFIX ${dep})
    find_package_python()
  elseif (dep STREQUAL "ZeroMQ")
    find_package_zmq()
  elseif (dep STREQUAL "rabbitmq-c")
    find_package_pkgconfig(rabbitmq-c HEADER amqp.h LIBNAMES rabbitmq)
  else()
    find_dependency(${dep})
  endif()
endforeach()
include(${CMAKE_CURRENT_LIST_DIR}/YggInterfaceTargets.cmake)
if (TARGET YggInterface::YggInterface)
  add_library(YggInterface::CXX ALIAS YggInterface::YggInterface)
  add_library(YggInterface::C ALIAS YggInterface::YggInterface)
endif()
if (TARGET YggInterface::YggInterface_fortran)
  add_library(YggInterface::Fortran ALIAS
              YggInterface::YggInterface_fortran)
endif()
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} CACHE INTERNAL "CMAKE_MODULE_PATH")
